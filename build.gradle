plugins {
    id 'io.jmix' version '1.2.0'
    id 'java'
}

apply plugin: 'org.springframework.boot'

jmix {
    bomVersion = '1.2.1'
}

group = 'com.company'
version = '0.0.1-SNAPSHOT'

bootRun {
    File mySecretPropertiesFile = rootProject.file("mysecrets.properties")
    File publicSecretPropertiesFile = rootProject.file("secrets.properties")
    File secretPropertiesFile = null;
    if (mySecretPropertiesFile.exists()) {
        secretPropertiesFile = mySecretPropertiesFile
    } else if (publicSecretPropertiesFile.exists()) {
        secretPropertiesFile = publicSecretPropertiesFile
    } else {
        throw new RuntimeException("没有找到隐私属性文件")
    }
    Properties secretProperties = new Properties();
    secretProperties.load(new FileInputStream(secretPropertiesFile))
    systemProperties(secretProperties)
}

dependencies {
    implementation 'io.jmix.core:jmix-core-starter'
    implementation 'io.jmix.data:jmix-eclipselink-starter'
    implementation 'io.jmix.ui:jmix-ui-starter'
    implementation 'io.jmix.ui:jmix-ui-data-starter'
    implementation 'io.jmix.ui:jmix-ui-themes-compiled'
    implementation 'io.jmix.ui:jmix-ui-widgets-compiled'
    implementation 'io.jmix.security:jmix-security-starter'
    implementation 'io.jmix.security:jmix-security-ui-starter'
    implementation 'io.jmix.security:jmix-security-data-starter'
    implementation 'io.github.pierresj:jmix-huaweifs-starter:1.0.7'
    implementation 'io.github.pierresj:jmix-qiniufs-starter:1.0.2'

    implementation 'org.springframework.boot:spring-boot-starter-web'

    runtimeOnly 'org.hsqldb:hsqldb'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.aliyun.com/nexus/content/groups/public/'
    }
    maven {
        url 'https://global.repo.jmix.io/repository/public'
    }
    maven {
        url = uri("https://maven.pkg.github.com/pierresj/jmix-qiniufs")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }
    maven {
        url = uri("https://maven.pkg.github.com/pierresj/jmix-huaweifs")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }


}

test {
    useJUnitPlatform()
}
